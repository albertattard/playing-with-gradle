plugins {
    id 'application'
}

// The application requires a command line argument, which can be passed using the `--args` followed by the arguments,
//  > ./gradlew run --args logflume
application {
    mainClass = 'aa.RideStatusService'
}

tasks.named('jar') {
    manifest {
        attributes('Main-Class': 'aa.RideStatusService')
    }

    // Use a custom Jar file name
    archiveFileName = 'theme-park.jar'
}

tasks.register('runJar', JavaExec) {
    classpath tasks.named('jar').map { it.outputs }
    classpath configurations.runtimeClasspath
    mainClass = 'aa.RideStatusService'
    args 'teacups'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

// Configure the verbose options to just one task with the name 'compileJava'
// tasks.named('compileJava') {
// or configure all tasks of type JavaCompile, which also includes tests (task 'compileTestJava')
tasks.withType(JavaCompile).configureEach {
    options.verbose = true;
}

// Filter the files copied by the 'processResources' task
tasks.named('processResources') {
    include '**/*.txt'
}
